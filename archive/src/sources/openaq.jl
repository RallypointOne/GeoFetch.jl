#--------------------------------------------------------------------------------# OpenAQ

module OpenAQ

import ..GeoFetch
using ..GeoFetch: AbstractDataSource, MetaData, DataAccessPlan, RequestInfo,
    _register_source!, _build_url, _get_api_key, _estimate_bytes,
    AIR_QUALITY, POINT, TemporalType, HTTPMethod, Frequency
using Dates

"""
    OpenAQ.Source()

Global air quality data from the [OpenAQ](https://openaq.org/) platform.

- **Coverage**: Global, 11,000+ stations
- **Resolution**: Station-based, hourly or daily aggregations
- **API Key**: Required (`OPENAQ_API_KEY` environment variable)
- **Rate Limit**: 60 requests/minute (free tier)

### Examples

```julia
ENV["OPENAQ_API_KEY"] = "your-api-key"

plan = DataAccessPlan(OpenAQ.Source(), (-87.6, 41.9),
    Date(2024, 1, 1), Date(2024, 1, 31);
    sensors = [1234],
    frequency = :daily)
files = fetch(plan)
```
"""
struct Source <: AbstractDataSource end

GeoFetch.name(::Type{Source}) = "openaq"

const URL = "https://api.openaq.org/v3"

const variables = (;
    pm25  = "PM2.5 (µg/m³)",
    pm10  = "PM10 (µg/m³)",
    o3    = "Ozone (µg/m³)",
    no2   = "Nitrogen dioxide (µg/m³)",
    so2   = "Sulfur dioxide (µg/m³)",
    co    = "Carbon monoxide (ppm)",
    bc    = "Black carbon (µg/m³)",
    pm1   = "PM1 (µg/m³)",
)

const metadata = MetaData(
    "OPENAQ_API_KEY", "60 req/min (free tier)",
    AIR_QUALITY, variables,
    POINT, "Station-based", "Global",
    TemporalType.timeseries, Hour(1), "Varies by station",
    "CC BY 4.0",
    "https://docs.openaq.org/";
    load_packages = Dict("DataFrames" => "a93c6f00-e57d-5684-b7b6-d8193f3e46c0"),
)

GeoFetch.MetaData(::Source) = metadata

function GeoFetch._request_headers(source::Source)
    api_key = _get_api_key(source)
    ["X-API-Key" => api_key]
end

function GeoFetch.DataAccessPlan(source::Source, extent, start_date::Date, stop_date::Date;
                                       sensors::Vector{Int} = Int[],
                                       frequency::Frequency.T = Frequency.daily,
                                       retention::Union{Nothing, Dates.Period} = metadata.default_retention)
    isempty(sensors) && error("OpenAQ requires sensor IDs via `sensors` keyword. " *
                              "Find sensors at https://explore.openaq.org/")

    endpoint = frequency == Frequency.daily ? "days" : "hours"
    date_from_key = frequency == Frequency.daily ? "date_from" : "datetime_from"
    date_to_key = frequency == Frequency.daily ? "date_to" : "datetime_to"

    requests = RequestInfo[]
    for sensor_id in sensors
        params = Dict{String, String}(
            date_from_key => Dates.format(start_date, dateformat"yyyy-mm-dd"),
            date_to_key   => Dates.format(stop_date, dateformat"yyyy-mm-dd"),
            "limit"       => "1000",
        )
        url = _build_url("$URL/sensors/$sensor_id/$endpoint", params)
        push!(requests, RequestInfo(source, url, HTTPMethod.GET, "Sensor $sensor_id, $endpoint"))
    end

    n_days = Dates.value(stop_date - start_date) + 1
    rows = frequency == Frequency.daily ? n_days : n_days * 24
    total_rows = rows * length(sensors)

    kwargs = Dict{Symbol, Any}(:sensors => sensors, :frequency => frequency)

    DataAccessPlan(source, requests, "$(length(sensors)) sensor(s)",
        (start_date, stop_date), collect(keys(variables)), kwargs,
        _estimate_bytes(total_rows, 1), retention)
end

_register_source!(Source())

end # module OpenAQ
